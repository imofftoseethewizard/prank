(module

 (include "./block-mgr-memory-proxy-imports.wam")
 (import "block-mgr" "init-blockset" (func $init-blockset (param i32 i32 i32)))
 (import "block-mgr" "get-block-ref-addr" (func $get-block-ref-addr (param i32) (result i32)))
 (import "block-mgr" "get-block-ref-size" (func $get-block-ref-size (param i32) (result i32)))

 (include "./globals.wam")
 (include "./block-mgr-memory-proxies.wam")

 (memory (export "memory") 1)

 (func $init (export "init")
   (param $blockset-id i32)

   ;; init blockset 0 st during defrag blocks >8KB are immobile, single blocks
   ;; between 4-8KB can be moved, and groups of blocks in aggregate < 4KB can be
   ;; moved.
   (call $init-blockset
         (local.get $blockset-id)
         (i32.const 0x2000)  ;; immobile block size
         (i32.const 0x1000)) ;; aggregate block move size limit

   (call $init-blockset-memory-proxies (local.get $blockset-id)))

 (func $fill (export "fill")
   (param $block-ref i32)
   (param $value i32)

   (memory.fill (call $get-block-ref-addr (local.get $block-ref))
                (local.get $value)
                (call $get-block-ref-size (local.get $block-ref))))

 (func $check-fill (export "check-fill")
   (param $block-ref i32)
   (param $value i32)
   (result i32)

   (local $addr i32)
   (local $end i32)

   (local.set $addr (call $get-block-ref-addr (local.get $block-ref)))
   (local.set $end (i32.add (local.get $addr)
                            (call $get-block-ref-size (local.get $block-ref))))

   (loop $again
     (if (i32.lt_u (local.get $addr) (local.get $end))
         (then
          (if (i32.eq (i32.load8_u (local.get $addr)) (local.get $value))
              (then
               (local.set $addr (i32.add (local.get $addr) (i32.const 1)))
               (br $again))))))

   (i32.eq (local.get $addr) (local.get $end)))


 )
